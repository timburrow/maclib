"addshapes - add two waveform shapes"
" usage: addshapes('shape1',multiplier1,phase1,'shape2',multiplier2,phase2,'output')"
" e.g., addshapes('gauss',1.0,0.0,'sinc',0.5,90.0,'sum')"
" If the two shapes are not the same number of steps, the resulting shape is as"
"  long as the longer of the two shapes, with the two shapes centered"
" The shapes are assumed to have no comments in them!"

write('line3','Adding waveforms ...')
$file1=userdir+'/shapelib/'+$1+'.RF'
$file2=userdir+'/shapelib/'+$4+'.RF'
$output=userdir+'/shapelib/'+$7+'.RF'
$mult[1]=$2 $mult[2]=$5
$addph[1]=$3 $addph[2]=$6

nrecords($file1):$numsteps[1]
nrecords($file2):$numsteps[2]
if $numsteps[1]<$numsteps[2] then
  $temp=$file1 $file1=$file2 $file2=$temp "swap names"
  $num=$numsteps[1] $numsteps[1]=$numsteps[2] $numsteps[2]=$num
  $num=$mult[1] $mult[1]=$mult[2] $mult[2]=$num "swap multipliers"
  $num=$addph[1] $addph[1]=$addph[2] $addph[2]=$num "swap phases"
endif

$pad=$numsteps[1]-$numsteps[2]
$padstart=trunc($pad/2) 
$i=1 repeat $phase1[$i]=0 $phase2[$i]=0 $amp1[$i]=0 $amp2[$i]=0 $i=$i+1 until $i>$numsteps[1]

$which=1
repeat
  if $which=1 then $i=1 else $i=$padstart+1 endif
  if $which=1 then $thefile=$file1 else $thefile=$file2 endif
  lookup('file',$thefile)
  repeat
    lookup('read',3):$phase,$amp,$gate 
    if $which=1 then
      $phase1[$i]=$phase $amp1[$i]=$amp
    else
      $phase2[$i]=$phase $amp2[$i]=$amp
    endif
    $i=$i+1
  until (($which=1) and ($i>$numsteps[1])) or (($which=2) and ($i>($numsteps[2]+$padstart)))
  $which=$which+1
until $which>2

$degtorad=2*3.14159/360
write('reset',$output)
$i=1
repeat "for each step in the shape"
  $x=0 $y=0 $which=1
  repeat
    if $which=1 then $deg=$phase1[$i]+$addph[1] else $deg=$phase2[$i]+$addph[2] endif
    if $which=1 then $amp=$amp1[$i]*$mult[1] else $amp=$amp2[$i]*$mult[2] endif
    if $deg>360 then $deg=$deg-360 endif
    if $deg<0 then $deg=$deg+360 endif
    $deg=$deg*$degtorad
    sin($deg):$x1 cos($deg):$y1
    $x=$x+$x1*$amp $y=$y+$y1*$amp
    $which=$which+1
  until $which>2
  $x=$x/2 $y=$y/2
  $r=sqrt($x*$x+$y*$y)
  if $y=0 then "either 90 or 270"
    $addphase=90
  else
    atan($x/$y):$addphase $addphase=$addphase/$degtorad
    if $addphase<0 then $addphase=-$addphase endif
    if $y<0 then $addphase=180-$addphase endif
  endif
  if $x<0 then $addphase=360-$addphase endif 
  write('file',$output,'%5.2f\t%6.1f\t%2.1f',$addphase,$r,1)
  $i=$i+1
until $i>$numsteps[1]
write('line3','Done!')
