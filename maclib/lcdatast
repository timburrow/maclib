"lcdatast - this version displays the LC-NMR 2000 format data or calls lcdatast_old"

$filename=curexp+'/lcdata'
exists($filename,'file'):$e
if $e=0 then
  write('line3','lcdata file does not exist in current experiment')
  abort
endif

"lcdata file exists"
lookup('file',$filename)
lookup('readline',2)
$version=''
$dum=''
lookup('read','readline'):$version,$dum
"write('line3',$version)"
if not $version = '2000' then 
  if $#>2 then lcdatast_old($1,$2,$3) 
  elseif $#>1 then lcdatast_old($1,$2)
  elseif $#>0 then lcdatast_old($1)
  else lcdatast_old
  endif
  return
endif

lookup('readline'):$filetext

$plotflag=(1=0) 
$fullflag=(1=0)
$sideflag=(1=0)
$det2flag=(det2='y')
$det2onflag=(1=0)

$vs=1/4
$vs2=1/4

exists('vslc','parameter'):$e
if $e then $vs=vslc/4 else $vs=1/4 endif
exists('vslc2','parameter'):$e
if $e then $vs2=vslc2/4 else $vs2=1/4 endif
$i=1
while $i<=$# do
  if ${$i} = 'plot' then $plotflag=(1=1) 
  elseif ${$i} = 'side' then $sideflag=(1=1) 
  elseif ${$i} = 'full' then $fullflag=(1=1)
  endif
  $i=$i+1
endwhile


exists('onflowdelay','parameter'):$e
if $e then $offset=onflowdelay/60 else $offset=0 endif
$plbs=(1=1)
exists('lcpeaklabels','parameter'):$e
if $e then $plbs=(lcpeaklabels='y') endif

if $plotflag then write('plotter','reverse',3,wc2/2,'%s',$filetext) endif
lookup('readline',7)

lookup('read','read','readline'):$text1,$text2,$text3   "active detectors"
"write('line3',$text2)"
$det2onflag=$text2='#TRUE#'

if not $det2onflag then $det2flag=(1=0) endif

lookup('readline',24)

lookup('read'):$numtimeevents
"write('alpha',$numtimeevents)"
$det=0 
$eventtime=0
$eventmv=0
$eventpoint=0
$event=1
$endevent=1
while ($event <= $numtimeevents) do
  $dum='' $r1=0 $r2=0 $r3=0 $r4=0 $r5=0 $r6=0
  lookup('read','read','read','read','read','read','read'):$r1,$r2,$r3,$r4,$r5,$r6,$r7

  "write('alpha','$r1=%s $r2=%s  $r3=%s $r4=%s $r5=%s',$r1,$r2,$r3,$r4,$r5)"
  
  $det[$event]=$r1
  $eventpoint[$event]=$r4
  $eventtime[$event]=$r5
  $eventmv[$event]=$r4

  $event=$event+1
  $endevent=$event-1
endwhile
$eventpoint[$event]=100000

$sum=1
$sum=sf1+wf1

if lcexp='stop' then
  $event=1
  $beginning=1
  $end=1
  repeat
    if ($eventtime[$event]<=sf1) then
      $beginning=$event+1
    endif
    $event=$event+1
  until $event=$endevent
endif



lookup('readline',3)
lookup('readline'):$numpoints
"write('alpha',$numpoints)"

"if (($fullflag=0) AND ($sideflag=1)) then $vs=$vs*(1-wc/wcmax) $vs2=$vs2*(1-wc/wcmax) endif"
if $sideflag then
  if $fullflag then
    $vs=$vs*wc/wcmax
  else
    $vs=$vs*(1-wc/wcmax) $vs2=$vs2*(1-wc/wcmax)
  endif
endif
if $plotflag then
  pen('plotter','pen1') $device='plotter'
else
  pen('graphics','yellow') $device='graphics'
endif
if $sideflag then 
  if $fullflag then $startmm=wcmax else $startmm=wcmax-wc-sc-15 endif
  $mmf=wc2/wf1
else 
  $startmm=wcmax-wc-sc
  $mmf=wc/wf1
endif
write($device,0,0,''):$htchar
if $plotflag then $wthchar=0.7*$htchar else $wthchar=0.5*$htchar endif
move($startmm,0)
if lcexp='stop' then
  $event=$beginning
else
  $event=1
endif
$y1s=0 $y2s=0 $x1s=$startmm $x2s=$startmm
$resok=4
$first=0
$skipnumber=$numpoints/1000
$skipnumber=trunc($skipnumber-1)


$rettime=1
$point=1
$mvolt=1
while ($point < $numpoints-$skipnumber-1) do
  $dum='' 
  if $skipnumber>0 then lookup('readline',$skipnumber):$dum endif
  lookup('read','read','read','readline'):$t,$a1,$a2,$dum
  $rettime=$t
"  write('alpha','$point=%d $t=%s $a1=%s  $a2=%s $dum=%s',$point,$t,$a1,$a2,$dum) "
  $mvolt=$a1
  $point=$point+1+$skipnumber
  $a1=$a1-oclc $a2=$a2-oclc2
  if ($t > sf1-$offset) then
    $mm=($t-sf1+$offset)*$mmf 
    if $sideflag then 
      $x1=$startmm-$a1*$vs $x2=$startmm-$a2*$vs2 $y1=$mm $y2=$y1 
    else 
      $x1=$startmm+$mm $x2=$x1 $y1=$a1*$vs $y2=$a2*$vs2 
    endif
     
    draw($x1,$y1) $y1s=$y1 $x1s=$x1 
    if $det2flag then
      move($x2s,$y2s) pen('$device','cyan') draw($x2,$y2) 
      $y2s=$y2 $x2s=$x2 pen('$device','yellow')
    endif 
    move($x1s,$y1s) 
    if $point >=$eventpoint[$event] then
      $s1=$eventtime[$event] $s2=$eventmv[$event] $s3=$eventmv[$event]
          
      if $sideflag then 
        pen('$device','green') 
        if $det[$event]=1 then
          move($x1-1,$y1) draw($x1+5,$y1)
	  move($x1,$y1) $xl=$x1 $s2=$mvolt
          if $plbs then
	    write($device,'red',$xl-12*$wthchar,$y1-$htchar/2,'%5.2fmin %3.0fmV',$s1,$s2)
	  endif
        elseif $det2flag then
          move($x2-1,$y2) draw($x2+5,$y2) move($x1,$y1) $xl=$x2
          if $plbs then
	    write($device,'cyan',$xl-12*$wthchar,$y1-$htchar/2,'%5.2fmin %3.0fmV',$s1,$s3)
	  endif
        endif
        pen('$device','yellow')   
        write($device,'cyan',$startmm,$y1-$htchar/2,'#%2.0f',$event)
        vp=$mm
        if $plotflag then
          pl($event)
          if $first=0 then pscale $first=1 endif
        else 
          dssn($event) 
          if $first=0 then dscale $first=1 endif
        endif
      else
        pen('$device','green') 
        if $det[$event]=1 then 
          move($x1,$y1+2) draw($x1,$y1-5) move($x1,$y1) $yl=$y1
          if $plbs then
	    write($device,'reverse','cyan',$x1-$htchar/1.5,$yl+$htchar,'%5.2fmin %3.0fmV',$s1,$s2)
	  endif
        elseif $det[$event]=2 then 
          move($x2,$y2+2) draw($x2,$y2-5) move($x2,$y1) $yl=$y2
          if $plbs then
	    write($device,'reverse','cyan',$x1-$htchar/1.5,$yl+$htchar,'%5.2fmin %3.0fmV',$s1,$s3)
	  endif
        endif 
        pen($device,'yellow') 
 
        write($device,'cyan',$x1-$htchar/1.5,0,'#%2.0f',$event)
      endif
      $event=$event+1 
    endif
  endif
  if ($rettime>$sum) then return endif 
endwhile
